buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.gradleup.shadow:shadow-gradle-plugin:8.3.0'
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.6'
    id 'org.jetbrains.kotlin.jvm' version '2.0.20'
    id("com.diffplug.spotless") version '6.25.0'
    id 'com.gradleup.shadow' version '8.3.0'
}

// This is optional, tells gradle that we want Java 11 compatible bytecode.
// Can be removed in standalone addons
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven {
        url 'https://maven.bookmap.com/maven2/releases/'
    }
}

dependencies {
    if (findProperty('is_built_from_main_bookmap_project')) {
        implementation fileTree(dir: "${main_libs}", include: ['*.jar'])
        implementation project(':Level1Api')
        implementation project(':SimplifiedApiWrapper')
    } else {
        compileOnly group: 'com.bookmap.api', name: 'api-core', version: '7.4.0.19'
        compileOnly group: 'com.bookmap.api', name: 'api-simplified', version: '7.4.0.19'

        compileOnly group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    }

    implementation group: 'io.reactivex.rxjava3', name: 'rxjava', version: '3.1.9'
    implementation group: 'io.reactivex.rxjava3', name: 'rxkotlin', version: '3.0.1'
}

jar {
    archiveFileName = 'quoteboard.jar'
}

shadowJar {
    archiveBaseName.set('quoteboard')
    archiveClassifier.set('')
    archiveVersion.set('')
    minimize()
}

kotlin {
    jvmToolchain(11)
}

spotless {
    kotlin {
        target("**/*.kt", "**/*.kts")
        ktfmt('0.52').kotlinlangStyle()
    }
}

eclipse.classpath.downloadJavadoc = true
idea.module.downloadJavadoc = true

if (idea.project) {
    // Create 'BookmapJar' run configuration for IntelliJ
    idea.project.settings.runConfigurations {
        def addOpensOptions = '--add-opens=java.base/java.lang=ALL-UNNAMED\n' +
                '--add-opens=java.base/java.io=ALL-UNNAMED\n' +
                '--add-opens=java.base/java.math=ALL-UNNAMED\n' +
                '--add-opens=java.base/java.util=ALL-UNNAMED\n' +
                '--add-opens=java.base/java.util.concurrent=ALL-UNNAMED\n' +
                '--add-opens=java.base/java.net=ALL-UNNAMED\n' +
                '--add-opens=java.base/java.text=ALL-UNNAMED\n' +
                '--add-opens=java.desktop/java.awt=ALL-UNNAMED\n' +
                '--add-opens=java.desktop/java.awt.color=ALL-UNNAMED\n' +
                '--add-opens=java.desktop/java.awt.peer=ALL-UNNAMED\n' +
                '--add-opens=java.desktop/com.sun.java.swing=ALL-UNNAMED\n' +
                '--add-opens=java.prefs/java.util.prefs=ALL-UNNAMED'
        if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            addOpensOptions += '--add-opens=java.desktop/sun.awt.windows=ALL-UNNAMED\n'
        }

        'BookmapJar'(org.jetbrains.gradle.ext.JarApplication) {
            // Change the path to Bookmap.jar here if you changed the default installation directory
            jarPath = '/Applications/Bookmap.app/Contents/app/Bookmap.jar'
            workingDirectory = '/Applications/Bookmap.app'
            jvmArgs = addOpensOptions
        }
    }
}
